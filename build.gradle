// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.61'

    final commonProp = new Properties()
    commonProp.load(new FileInputStream(new File(rootDir, "common.properties")))
    println("common.properties:\n$commonProp")
    ext.commonProp = commonProp

    ext.buildInfo = [
            'compileSdkVersion': Integer.parseInt(commonProp['compileSdkVersion']),
            'buildToolsVersion': commonProp['buildToolsVersion'],
            'versionCode'      : Integer.parseInt(commonProp['versionCode']),
            'versionName'      : commonProp['versionName'],
            'targetSdkVersion' : Integer.parseInt(commonProp['targetSdkVersion']),
            'minSdkVersion'    : Integer.parseInt(commonProp['minSdkVersion'])
    ]

    ext.android_gradle_version = '3.5.3'

    boolean testLocal = commonProp["testInjectPluginLocal"] == "true"

    final privateProp = new Properties()
    final privateFile = new File(rootDir, "private.properties")
    ext.publishAppInjectModule = privateFile.exists() && commonProp["enablePublishAppInject"] == "true"
    ext.publishPlugin = privateFile.exists() && commonProp['enablePublishPlugin'] == "true"
    if (publishAppInjectModule || publishPlugin) {
        privateProp.load(new FileInputStream(privateFile))
        println("private.properties:\n$privateProp")
        ext.privateProp = privateProp
    }

    repositories {
        if (testLocal && commonProp['testRemoteRepo'] != "true") {
            maven {
                url uri("repo/")
            }
        }
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${android_gradle_version}"

        if (testLocal) {
            classpath "${commonProp['group']}:${commonProp['pluginId']}:${buildInfo['versionName']}"
        } else if (commonProp['testRemoteRepo'] == "true") {
            // remote repository
            classpath 'com.lyc:app-inject-plugin:0.4.0'
        }

        if (publishAppInjectModule || publishPlugin) {
            classpath 'com.novoda:bintray-release:0.9.2'
        }

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
